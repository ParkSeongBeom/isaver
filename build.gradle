//=================================================================//
// gradle-multi ROOT
//=================================================================//

buildscript {
    ext {
        springBootVersion = '2.3.7.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE')
    }
}

allprojects {
    group = 'com.icent.isaver'
    version = '1.7.5'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }

        sourceSets*.resources.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Logging
        implementation 'org.slf4j:slf4j-api:1.7.25'
        implementation 'ch.qos.logback:logback-classic:1.2.3'

        // Devtools
        developmentOnly 'org.springframework.boot:spring-boot-devtools'

        // Guava
        implementation 'com.google.guava:guava:28.0-jre'

        // Validation
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}